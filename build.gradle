buildscript {
    ext {
        junit_version = '5.0.3'
        junit_platform_version = '1.1.0'
        kotlin_version = '1.2.30'
        publish_version = '0.9.10'
    }

    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:$publish_version"
    }
}


buildscript {
    ext.error = false
    if (!ext.error) {
        apply from: "${rootProject.projectDir}/gradle/scripts/localRepo.gradle"
        includeLocalRepo(gradle.ext.repo, 'junitjacoco', gradle.ext.version, this)
        includeLocalRepo(gradle.ext.repo, 'project-utils', gradle.ext.version, this)
    }

    if (ext.error || !ext.plugin_junitjacoco_exists) {
        dependencies {
            classpath "ch.tutteli:tutteli-gradle-junitjacoco:${gradle.ext.previous_version}"
        }
    }
}

version = gradle.ext.version
group = 'ch.tutteli'

subprojects {
    apply plugin: 'groovy'
    version = rootProject.version
    group = rootProject.group

    repositories {
        mavenCentral()
        jcenter()
    }
}

def pluginProjects = subprojects - project('test-utils')
configure(pluginProjects) {
    apply plugin: 'java-gradle-plugin'
    apply plugin: 'com.gradle.plugin-publish'
}

configure(pluginProjects) {

    apply plugin: 'ch.tutteli.junitjacoco'

    junitjacoco {
        //enable or disable junit xml (disabled by default, enable it as follows)
        enableJunitReport = true

        //allows to configure the JacocoReportTask
        jacocoReport {
            reports {
                html.enabled = true
            }
        }

        //delegates to the jacoco extension (just for your convenience, everything in one place)
        jacoco {
            toolVersion = '0.8.0'
        }

        //delegates to the junitPlatform extension (just for your convenience, everything in one place)
        junitPlatform {
            reportsDir = file("${buildDir}/reports/junit")
        }
    }

    if (!rootProject.ext.error && rootProject.ext.'plugin_project-utils_exists') {
        apply plugin: 'ch.tutteli.project.utils'
    }
}

configure(pluginProjects) { subproject ->

    //TODO can be removed if plugin DSL is also applicable to settings.gradle
    def createClasspathManifest = task('createClasspathManifest') {
        def outputDir = file("${buildDir}/${name}")

        inputs.files sourceSets.main.runtimeClasspath
        outputs.dir outputDir

        doLast {
            outputDir.mkdirs()
            file("${outputDir}/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
        }
    }
    test.dependsOn(createClasspathManifest)

    dependencies {
        testCompile(project(':test-utils')) {
            exclude group: 'org.codehaus.groovy'
        }
        testCompile("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testRuntime("org.junit.jupiter:junit-jupiter-engine:$junit_version")
        testRuntime files(createClasspathManifest) //required for settings.gradle test
        testCompile("org.mockito:mockito-core:2.15.0")
    }

    afterEvaluate {
        gradlePlugin {
            plugins {
                tutteliPlugin { //name does not matter
                    id = subproject.ext.plugin_id
                    implementationClass = subproject.ext.plugin_class
                }
            }
        }

        pluginBundle {
            website = 'https://github.com/robstoll/tutteli-gradle-plugins'
            vcsUrl = 'https://github.com/robstoll/tutteli-gradle-plugins.git'

            plugins {
                tutteliPlugin { //name does not matter
                    id = subproject.ext.plugin_id
                    displayName = subproject.ext.plugin_name
                    description = subproject.ext.plugin_description
                    tags = subproject.ext.plugin_tags
                }
            }

            mavenCoordinates {
                groupId = subproject.group
            }
        }
    }

    apply plugin: 'maven-publish'

    task repo(type: Copy) {
        def outputJar = "${gradle.ext.repo}/ch/tutteli/${project.name}/${project.version}/"
        outputs.dir outputJar
        from jar
        from tasks.matching { it.name == 'generatePomFileForPluginMavenPublication' }
        rename('pom-default.xml', "${project.name}-${project.version}.pom")
        into outputJar
    }
}

task removeRepo(type: Delete) {
    delete gradle.ext.repo
}

/*
Release & deploy
----------------
1. Update master:
    a) point to the tag
        1) search for `branch=master` and replace it with `tag=vX.Y.Z` (build status and coverage in README.md)
        2) search for `tree/master` and replace it with `tree/vX.Y.Z` (README.md)
    b) change `version` in settings.gradle to X.Y.Z (remove -SNAPSHOT)
    c) commit (modified build.gradle, README.md and settings.gradle)
    d) git tag vX.Y.Z
    e) git push origin vX.Y.Z
2. publish plugins
   a) gr publishPlugins
3. create release on github


Prepare next dev cycle
-----------------------
1. change version in settings.gradle to X.Y.Z-SNAPSHOT
2. change previous_version in settings.gradle
3. point to master
   a) search for `tag=vX.Y.Z` and replace it with `branch=master`
   b) search for `tree/vX.Y.Z` and replace it with `tree/master`
4. commit & push changes

*/
